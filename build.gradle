plugins {
    id "java"
    id "maven-publish"
    id "io.freefair.lombok" version "6.5.1"
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id "com.github.ben-manes.versions" version "0.42.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    graphqlVersion = '14.0.0'
    mapstructVersion = '1.5.0.RC1'
    mapstructBindingVesion = '0.2.0'
    guavaVersion = '31.1-jre'
    junitVersion = '5.9.0'
    embedMongoVersion = '3.4.5'
    reactorTestVersion = '3.4.22'
    hibernateValidatorVersion = '7.0.5.Final'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb-reactive"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-security"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.springframework.boot:spring-boot-starter-undertow'

    implementation "com.graphql-java-kickstart:graphql-spring-boot-starter:${graphqlVersion}"

    implementation "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"
    implementation "org.hibernate:hibernate-validator-annotation-processor:${hibernateValidatorVersion}"

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${mapstructBindingVesion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "io.projectreactor:reactor-test:${reactorTestVersion}"
    testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:${embedMongoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "com.graphql-java-kickstart:graphql-spring-boot-starter-test:${graphqlVersion}"

}

group = "pl.maciejkopec"
version = "1.0.0-SNAPSHOT"
description = "cms"
sourceCompatibility = '17'

tasks.withType(JavaCompile) {
    options.compilerArgs += "-Amapstruct.defaultComponentModel=spring"
    options.compilerArgs += "--enable-preview"
    options.encoding = "UTF-8"

}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    builder = "micneo/cms"
}

jar {
    enabled = false
}

compileJava.inputs.files(processResources)
